#version 430 compatibility
#extension GL_ARB_COMPUTE_SHADER : enable
#extension GL_ARB_SHADER_STORAGE_BUFFER_OBJECT : enable
#extension GL_EXT_gpu_shader4: enable

struct Particle
{
	vec3 mPos;
	float speed;
	float life;
	float currentlife;
	vec3 direction;
};

layout(std430, binding = 2) buffer ParticleDataBlock
{
	Particle each_particle[];
};

struct ParticleData
{
	mat4 mModel;
	vec4 more_data;
};

layout(std430, binding = 3) buffer ModelBlock
{
	ParticleData data[];
};

layout(std430, binding = 4) buffer Randoms
{
	vec4 randoms[];
};

layout(std430, binding = 5) buffer Vertices
{
	vec3 vertex[];
};

layout(local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

uniform float DeltaTime;
uniform vec2 SpeedRange;
uniform vec2 ScaleRange;
uniform vec2 LifeRange;
uniform vec3 Center;
uniform vec3 MinColor;
uniform vec3 MaxColor;
uniform bool Pause;
uniform bool Active;
uniform mat4 Model;
uniform int ActiveParticleCount;
uniform int RandomNumberSize;

highp float rand(vec2 co)
{
	highp float a = 12.9898;
	highp float b = 78.233;
	highp float c = 43758.5453;
	highp float dt = dot(co.xy, vec2(a, b));
	highp float sn = mod(dt, 3.14);
	return fract(sin(sn) * c);
}

int GetIndex(float rand)
{
	return int(rand * RandomNumberSize/2);
}

void main()
{
	uint gid = gl_GlobalInvocationID.x;
	int index;
	each_particle[gid].currentlife = each_particle[gid].currentlife - DeltaTime;
	vec3 pos = each_particle[gid].mPos;


	mat4 m = mat4(0.5f);
	m[3].w = 1.0f;
	m = Model * m;

	if(!Pause && gid < ActiveParticleCount)
	{
		if(Active)
		{
			if(each_particle[gid].currentlife <= 0)
			{
				// Sphere spawning
				vec3 p = vertex[gid];
				
				pos = (m * vec4(p,1.0f)).xyz;

				index = GetIndex(rand(pos.yx));
				each_particle[gid].life = randoms[index].x * (LifeRange.y - LifeRange.x) + LifeRange.x;
				each_particle[gid].currentlife = each_particle[gid].life;
				each_particle[gid].speed = 1.0f;
			}
		}
		else
		{
			each_particle[gid].life = 0.0f;
			each_particle[gid].currentlife = 0.0f;
			each_particle[gid].speed = 0.0f;
			each_particle[gid].direction = vec3(0.0f);
			each_particle[gid].mPos = vec3(0.0f);
		}
	}

	index = GetIndex(rand(pos.zy));
	float scale = randoms[index].y * (ScaleRange.y - ScaleRange.x) + ScaleRange.x;
	mat4 temp = mat4(scale);
	temp[3] = vec4(pos,1.0f);

	each_particle[gid].mPos = pos;

	data[gid].mModel = temp;
	data[gid].more_data.x = each_particle[gid].speed;
	float factor = each_particle[gid].currentlife / each_particle[gid].life;
	data[gid].more_data.yzw = mix(MinColor,MaxColor,factor);
}